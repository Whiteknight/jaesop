namespace JavaScript[HLL]
{
    const string ATTR_NAME_PROTOTYPE = "prototype";
    const string ATTR_NAME_CONSTRUCTOR = "constructor";
    const string ATTR_NAME_ATTRIBUTES = "attributes";
    const string ATTR_NAME_HATTRIBUTES = "hidden_attributes";
    const string PROP_NAME_CODE = "!!!code!!!";
    const string PROP_NAME_JSFUNC = "!!!jsfunction!!!";

    namespace JSObject
    {
        function error(string fmt, var args[slurpy])
        {
            die(sprintf("JSObject: " + fmt, args));
        }

        function get_prototype(var jsobj)
        {
            int isa_jsobj = 0;
            var jsobj_class = class JSObject;
            ${ isa isa_jsobj, jsobj, jsobj_class };
            if (isa_jsobj) {
                var proto = null;
                ${ getattribute proto, jsobj, jsobj_class, ATTR_NAME_PROTOTYPE };
                return proto;
            }
            return null;
        }

        function get_constructor(var jsobj)
        {
            int isa_jsobj = 0;
            var jsobj_class = class JSObject;
            ${ isa isa_jsobj, jsobj, jsobj_class };
            if (isa_jsobj) {
                var proto = null;
                ${ getattribute proto, jsobj, jsobj_class, ATTR_NAME_CONSTRUCTOR };
                return proto;
            }
            return null;
        }

        function box_func(var f)
        {
            if (f instanceof JSFunction)
                return f;
            var jsf;
            ${ getprop jsf, PROP_NAME_JSFUNC, f };
            if (jsf == null)
                jsf = new JSFunction(f);
            return jsf;
        }

        function construct(var constructor, var args [slurpy])
        {
            var con_proto = get_prototype(constructor);
            var jsobj = new JSObject(con_proto, constructor);
            jsobj.*constructor(args:[flat]);
            return jsobj;
        }

        function invoke(var obj, var args [slurpy])
        {
            var f = null;
            ${ getprop f, PROP_NAME_CODE, obj };
            if (f == null)
                error("Cannot invoke non-code object");
            return obj.*f(args);
        }
    }

    class JSObject
    {
        var attributes;          // Attributes that the user can enumerate
        var hidden_attributes;   // Attributes that cannot be enumerated

        function JSObject(var prototype, var constructor, var values [slurpy,named])
        {
            var jsobj_class = class JSObject;
            var hattrs = {
                ATTR_NAME_PROTOTYPE : prototype,
                ATTR_NAME_CONSTRUCTOR : constructor
            };
            ${ setattribute self, jsobj_class, ATTR_NAME_HATTRIBUTES, hattrs };
            ${ setattribute self, jsobj_class, ATTR_NAME_ATTRIBUTES, values };
        }

        function get_attr_str[vtable](string key)
        {
            var jsobj_class = class JSObject;

            // Check the visible attrs. If we have it there, return it.
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            if (exists attrs[key])
                return attrs[key];

            // Check the hidden attrs. If we have it, return it.
            var hattrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_HATTRIBUTES };
            if (exists hattrs[key])
                return hattrs[key];

            // If not, we can try to look in the prototype
            if (key != ATTR_NAME_PROTOTYPE && (exists hattrs[ATTR_NAME_PROTOTYPE])) {
                var proto = hattrs[ATTR_NAME_PROTOTYPE];
                if (proto != null)
                    return proto.*key;
            }
            return null;
        }

        function set_attr_str[vtable](string key, var value)
        {
            var jsobj_class = class JSObject;

            // First, if it's the name of a hidden attribute, update that. We
            // don't want to shadow these. Overwriting "prototype" and
            // "constructor" hidden attrs is fine. Overwriting others, maybe
            // not so much.
            var hattrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_HATTRIBUTES };
            if (exists hattrs[key]) {
                hattrs[key] = value;
                return;
            }

            // Next, set it like a regular attr, overwriting if necessary.
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            attrs[key] = value;
        }

        function get_pmc_keyed[vtable](var key)
        {
            return self.*(string(key));
        }

        function set_pmc_keyed[vtable](var key, var value)
        {
            self.*(string(key)) = value;
        }

        function does[vtable](string role)
        {
            if (role == "hash")
                return true;
            return false;
        }

        function get_iter[vtable]()
        {
            var jsobj_class = class JSObject;
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            var attrs_iter;
            ${ iter attrs_iter, attrs };
            return attrs_iter;
        }
    }

    class JSFunction : JSObject
    {
        function JSFunction(var func)
        {
            self.JSObject();
            ${ setprop self, PROP_NAME_CODE, func };
            ${ setprop func, PROP_NAME_JSFUNC, self };
        }
    }
}
