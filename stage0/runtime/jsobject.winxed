namespace JavaScript[HLL]
{
    const string ATTR_NAME_PROTOTYPE = "prototype";
    const string ATTR_NAME_CONSTRUCTOR = "constructor";
    const string ATTR_NAME_ATTRIBUTES = "attributes";
    const string PROP_NAME_JSFUNC = "!!!jsfunction!!!";

    /* Global value store
    */

    function global_store()
    {
        var store = {};
        while(1)
            yield store;
    }

    function store_global(string name, var jsobj)
    {
        global_store()[name] = jsobj;
    }

    function fetch_global(string name)
    {
        return global_store()[name];
    }

    /* JSObject
    */

    // JSObject helper methods
    namespace JSObject
    {
        function error(string fmt, var args[slurpy])
        {
            die(sprintf("JSObject: " + fmt, args));
        }

        function box_function(var f, var proto [optional], int has_proto [opt_flag])
        {
            if (f instanceof JSObject)
                return f;

            var jsf;
            ${ getprop jsf, PROP_NAME_JSFUNC, f };
            if (jsf == null) {
                var function_constructor = fetch_global("Function");
                jsf = construct(proto, function_constructor, f);

                // Cache the JSObject as a property on the Sub, so we don't
                // try to re-box it later
                ${ setprop f, PROP_NAME_JSFUNC, jsf };
            }
            return jsf;
        }

        function construct(var proto, var constructor,
                var constr_args [slurpy],
                var jsobj_values [slurpy,named])
        {
            if (!(constructor instanceof JSObject))
                constructor = box_func(constructor, null);

            if (proto == null)
                proto = get_prototype(constructor);
            var jsobj = new JSObject(proto, constructor, jsobj_values:[flat,named]);
            constructor(jsobj, constr_args:[flat]);
            return jsobj;
        }
    }

    // JSObject class definition. All objects in JavaScript are JSObjects with
    // different constructors and prototypes
    class JSObject
    {
        var attributes;

        function JSObject(var prototype, var constructor, var values [slurpy,named])
        {
            var jsobj_class = class JSObject;
            if (prototype == null && constructor != null)
                prototype = constructor.*"prototype";
            var attrs = {
                ATTR_NAME_PROTOTYPE : prototype,
                ATTR_NAME_CONSTRUCTOR : constructor
            };
            for (string key in values)
                attrs[key] = values[key];
            ${ setattribute self, jsobj_class, ATTR_NAME_ATTRIBUTES, attrs };
        }

        /* VTABLES
            These implement most of the JS-visible behaviors of JSObject
        */

        function get_attr_str[vtable](string key)
        {
            var jsobj_class = class JSObject;
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            if (exists attrs[key])
                return attrs[key];

            // If not, we can try to look in the prototype
            if (key != ATTR_NAME_PROTOTYPE && (exists attrs[ATTR_NAME_PROTOTYPE])) {
                var proto = attrs[ATTR_NAME_PROTOTYPE];
                if (proto != null)
                    return proto.*key;
            }
            return null;
        }

        function set_attr_str[vtable](string key, var value)
        {
            var jsobj_class = class JSObject;
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            attrs[key] = value;
        }

        function get_pmc_keyed[vtable](var key)
        {
            return self.*(string(key));
        }

        function set_pmc_keyed[vtable](var key, var value)
        {
            self.*(string(key)) = value;
        }

        function get_pmc_keyed_int[vtable](int key)
        {
            var getf = self.*"__GET_PMC_IDX__";
            if (getf != null)
                return self.*getf(key);
            return self.*(string(key));
        }

        function set_pmc_keyed_int[vtable](int key, var value)
        {
            var setf = self.*"__SET_PMC_IDX__";
            if (setf != null)
                self.*setf(key, value);
            else
                self.*(string(key)) = value;
        }

        function does[vtable](string role)
        {
            var roles = self.*"__DOES_ROLEs__";
            if (roles == null)
                return false;
            for (string r in roles) {
                if (r == role)
                    return true;
            }
            return false;
        }

        function get_iter[vtable]()
        {
            var iterf = self.*"__GET_ITER__";
            if (iterf != null)
                return self.*iterf();
            var jsobj_class = class JSObject;
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            var attrs_iter;
            ${ iter attrs_iter, attrs };
            return attrs_iter;
        }

        function invoke[vtable](var this, var args [slurpy])
        {
            var f = self.*"__CODE_PMC__";
            if (f == null) {
                string type_name = self.get_type_name();
                error("Cannot invoke object of type " + type_name);
            }
            var result = f(this, args:[flat]);
            return result;
        }

        /* Helper Methods
            These methods are for internal use only. They are not visible from
            JS code. Methods callable from JS are stored in the attrs hash.
        */

        function get_type_name()
        {
            var constructor = self.*"constructor";
            if (constructor == null)
                return "Object";
            var toString = constructor.*"toString";
            return toString(constructor);
        }

        function get_prototype()
        {
            return self.*"prototype";
        }

        function get_constructor()
        {
            self.*"constructor";
        }

        function get_attribute_hash()
        {
            var jsobj_class = class JSObject;
            var attrs;
            ${ getattribute attrs, self, jsobj_class, ATTR_NAME_ATTRIBUTES };
            return attrs;
        }
    }

    /* Built-in types
    */

    // Function is the root type. It provides basic invokable behavior and
    // a few other details.
    namespace Function
    {
        function get_Function_prototype[anon]()
        {
            var p = new JSObject(null, null);
            return p;
        }

        function setup_Function_prototype[anon](var p)
        {
            using JavaScript.JSObject.box_function;

            p.*"toString" = box_function(function(var obj) {
                var code_pmc = obj.*"__CODE_PMC__";
                if (code_pmc != null)
                    return string(code_pmc);
                string type_name = obj.get_type_name();
                string r = "[Object " + type_name + "]";
                return r;
            });

            p.*"call" = box_function(function(var func, var this, var args[slurpy]) {
                return func(this, args:[flat]);
            });

            p.*"apply" = box_function(function(var func, var this, var args) {
                return func(this, args:[flat]);
            });

            p.*"__DOES_ROLES__" = ["hash"];
        }

        function Function(var obj, var sub_code)
        {
            obj.*"__CODE_PMC__" = sub_code;
            obj.*"length" = sub_code.arity();
        }
    }

    // Object is what most objects are.
    namespace Object
    {
        function get_Object_prototype[anon](var func_proto)
        {
            using JavaScript.JSObject.box_function;

            var p = new JSObject(func_proto, null);

            p.*"hasOwnProperty" = box_function(function(var obj, string key) {
                var attrs = obj.get_attribute_hash();
                return exists attrs[key] ? true : false;
            });

            p.*"getPrototypeOf" = box_function(function(var obj) {
                return obj.get_prototype();
            });

            return p;
        }

        function Object(var obj)
        {
        }
    }

    // Array is for low-level integer-keyed arrays. It's a thick wrapper around
    // ResizablePMCArray
    namespace Array
    {
        function get_array_contents[anon](var jsobj)
        {
            var a = get_attribute_hash(jsobj);
            return a["__ARRAY_CONTENTS__"];
        }

        function get_Array_prototype[anon](var obj_proto)
        {
            using JavaScript.JSObject.box_function;
            var p = new JSObject(obj_proto, null);
            var a = p.get_attribute_hash();

            a["__GET_PMC_IDX__"] = box_function(function(var obj, int key) {
                var c = get_array_contents(obj);
                return c[key];
            });

            a["__SET_PMC_IDX__"] = box_function(function(var obj, int key, var value) {
                var c = get_attribute_hash(obj);
                c[key] = value;
                obj.*"length" = elements(c);
            });

            a["__GET_ITER__"] = box_function(function(var obj) {
                // TODO: Actually, we need to iterate over the array contents
                //       AND the rest of the JSObject attributes
                var c = get_attribute_hash(obj);
                var c_iter;
                ${ iter c_iter, c };
                return c_iter;
            });

            a["__DOES_ROLES__"] = ["array", "hash"];
        }

        function Array(var obj, var args [slurpy])
        {
            var a = get_attribute_hash(obj);
            a["__ARRAY_CONTENTS__"] = [];
            a["length"] = 0;
        }
    }

    /* Setup routine
        Setup the various built-in types
    */

    function __BOOTSTRAP_FUNCTION__[anon]()
    {
        using JavaScript.Function.Function;
        var func_proto = JavaScript.Function.get_Function_prototype();
        var func = new JSObject(func_proto, null);
        ${ setprop Function, PROP_NAME_JSFUNC, func };
        func.*"constructor" = func;
        Function(func, Function);
        store_global("Function", func);
        JavaScript.Function.setup_Function_prototype(func_proto);
        return func_proto, func;
    }

    function __SETUP__[anon,init,load]()
    {
        var func_proto;
        var func;
        :(func_proto, func) = __BOOTSTRAP_FUNCTION__();

        using JavaScript.Object.Object;
        var obj_proto = JavaScript.Object.get_Object_prototype(func_proto);
        var obj = JavaScript.JSObject.box_function(Object, obj_proto);
        store_global("Object", obj);

        using JavaScript.Array.Array;
        var array_proto = JavaScript.Array.get_Array_prototype(obj_proto);
        var array = JavaScript.JSObject.box_function(Array, array_proto);
        store_global("Array", obj);
    }
}
